plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
    id 'org.sonarqube' version '5.1.0.4882'
}

group = 'com.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'io.cucumber:cucumber-java:6.0.0'
    testImplementation 'io.cucumber:cucumber-junit:6.0.0'
    testImplementation 'junit:junit:4.13.2'
}

test {
    useJUnit()
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

sonarqube {
    properties {
        property 'sonar.projectKey', 'TP7_Jenkins'
        property 'sonar.projectName', 'TP7 Jenkins Pipeline'
        property 'sonar.host.url', 'http://localhost:9000/'
        property 'sonar.sources', 'src/main/java'
        property 'sonar.tests', 'src/test/java'
        property 'sonar.java.binaries', 'build/classes/java/main'
        property 'sonar.java.libraries', configurations.compileClasspath.files.join(',')
        property 'sonar.junit.reportPaths', 'build/test-results/test'
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml'
        property 'sonar.token', System.getenv('SONAR_TOKEN') ?: '1073ae6f6c62583a30a542b9c8118b60faac8fde'
    }
}

publishing {
    repositories {
        maven {
            url = uri(System.getenv('MAVEN_REPO_URL') ?: 'http://mymavenrepo.com/repo/cEmjfkxugPlzLxXg1A2B/')
            allowInsecureProtocol = true
            credentials {
                username = System.getenv('MAVEN_USERNAME') ?: 'myMavenRepo'
                password = System.getenv('MAVEN_PASSWORD') ?: 'test0005'
            }
        }
    }

    publications {
        maven(MavenPublication) {
            from components.java
            groupId = 'com.example'
            artifactId = 'tp7-jenkins'
            version = '1.0.0'
        }
    }
}

// Tâche pour générer le JAR
jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}

// Tâche pour générer la documentation Javadoc
javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
}

// Finaliser les tests avec le rapport JaCoCo
tasks.named('test') {
    finalizedBy 'jacocoTestReport'
}